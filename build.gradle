plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/peculiarpieces.accesswidener")
}

repositories {
    maven { url = "https://jitpack.io/" }
    maven { url = "https://ladysnake.jfrog.io/artifactory/mods" }
    maven { url = "https://maven.blamejared.com" }
    maven { url = "https://maven.terraformersmc.com/" }
    maven { url = "https://maven.willbl.dev/releases" }
    maven { url = "https://maven.siphalor.de" }
    maven { url = "https://maven.jamieswhiteshirt.com/libs-release" }
    maven { url = "https://maven.bai.lol" }
}

dependencies {
    //Fabric
    minecraft ("com.mojang:minecraft:${project.minecraft_version}")
    mappings ("net.fabricmc:yarn:${project.yarn_mappings}:v2")
    modImplementation ("net.fabricmc:fabric-loader:${project.loader_version}")
    modImplementation ("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
    //External
    modImplementation ("vazkii.patchouli:Patchouli:1.19-75-FABRIC-SNAPSHOT")
    modImplementation ("dev.emi:trinkets:${project.trinkets_version}")
    modImplementation ("amymialee:visible-barriers:1.5.2+1.19")
    modImplementation ("amymialee:kofi-table:1.0.4+1.19.x")
    modImplementation ("com.terraformersmc:modmenu:4.0.6")
    modCompileOnly "mcp.mobius.waila:wthit-api:fabric-5.12.0"
    modRuntimeOnly "mcp.mobius.waila:wthit:fabric-5.12.0"
    modRuntimeOnly "lol.bai:badpackets:fabric-0.2.0"
    //Include
    modImplementation include ("de.siphalor:nbtcrafting-1.19:2.2.3+mc1.19")
    modImplementation include ("io.github.ladysnake:PlayerAbilityLib:1.6.0")
    modImplementation include ("com.jamieswhiteshirt:reach-entity-attributes:2.3.0")
    modImplementation include ("dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.0.1")
    modImplementation include ("dev.onyxstudios.cardinal-components-api:cardinal-components-chunk:5.0.1")
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "willBlRepositoryReleases"
            url = "https://maven.willbl.dev/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}